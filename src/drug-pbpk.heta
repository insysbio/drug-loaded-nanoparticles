/*
    This is a coremodel of drug (vincristine) distribution in mouse.
*/

kdiffUnits #defineUnit {
    units: [{kind: h, exponent: -0.43}]
}; 

t {units: h};
'''average tissues density'''
rho_avg @Record {units: g/mL} := 1;

// compartments
block @Compartment begin
    V_B {units: mL} 'Blood volume' := W_animal/rho_avg * (w_B + w_L*bto_L + w_S*bto_S + w_K*bto_K + w_U*bto_U) + W_T/rho_avg * bto_T;
    V_L {units: mL} 'Liver volume' := W_animal/rho_avg * w_L*(1-bto_L);
    V_S {units: mL} 'Spleen volume' := W_animal/rho_avg * w_S*(1-bto_S);
    V_T {units: mL} 'Tumor colume' := W_T/rho_avg * (1-bto_T);
    V_K {units: mL} 'Kidneys volume' := W_animal/rho_avg * w_K*(1-bto_K);
    V_U {units: mL} 'Lungs volume' := W_animal/rho_avg * w_U*(1-bto_U);
    V_R {units: mL} 'Rest of body (ROB) volume' := W_animal/rho_avg * w_R;

    V_P {units: mL} 'Volume of blood plasma' := V_B * (1-hemato);
end

// species
block @Species begin
    D_B 'Free drug in blood' {compartment: V_B, units: ug/mL} .= 0;
    D_L 'Free drug in liver' {compartment: V_L, units: ug/mL} .= 0;
    D_S 'Free drug in spleen' {compartment: V_S, units: ug/mL} .= 0;
    D_T 'Free drug in tumor' {compartment: V_T, units: ug/mL} .= 0;
    D_K 'Free drug in kidneys' {compartment: V_K, units: ug/mL} .= 0;
    D_U 'Free drug in lungs' {compartment: V_U, units: ug/mL} .= 0;
    D_R 'Free drug in ROB' {compartment: V_R, units: ug/mL} .= 0;
end

// dynamic records
response @Record {units: 1} .= 0;

// doses
'''Switcher describing single dose at 0 time.'''
single_dose_sw @TimeSwitcher { start: 0 };
dose_D_ug @Record {units: ug/mL} := delta * dose_D_ug_g * W_animal/V_B;
D_B [single_dose_sw]= dose_D_ug;

// records
block @Record begin
    D_P {units: ug/mL} := D_B / (hemato*Kp_BC_D+1-hemato);

    // PS_L := PS_L_061;
    PS_L {units: mL/h} := PS_L_061 * pow(w_L*W_animal/w_L_061/W_animal_061, b_PS) * exp(-koef_resp_L*response);
    // PS_S := PS_S_061;
    PS_S {units: mL/h} := PS_S_061 * pow(w_S*W_animal/w_S_061/W_animal_061, b_PS);
    // PS_T := PS_T_061;
    PS_T {units: mL/h} := PS_T_061 * pow(W_T/W_T_061, b_PS);
    // PS_K := PS_K_061;
    PS_K {units: mL/h} := PS_K_061 * pow(w_K*W_animal/w_K_061/W_animal_061, b_PS) * exp(-koef_resp_K*response);
    // PS_U := PS_U_061;
    PS_U {units: mL/h} := PS_U_061 * pow(w_U*W_animal/w_U_061/W_animal_061, b_PS);
    // PS_R := PS_R_061;
    PS_R {units: mL/h} := PS_R_061 * pow(w_R*W_animal/w_R_061/W_animal_061, b_PS) * exp(-koef_resp_R*response);

    w_R {units: 1} := 1 - w_L - w_S - w_K - w_U - w_B - W_T/W_animal; // {--} weight ratio of rest of body
    w_R_061 {units: 1} := 1 - w_L_061 - w_S_061 - w_K_061 - w_U_061 - w_B_061 - W_T_061/W_animal_061;

    Q_T @Record {units: mL/h} := Q_T_g * W_T;              // {ml/h}
end

// reactions
vdist_D_L @Reaction {actors: D_B <=> D_L, units: ug/h};
vdist_D_L := (Q_L-Q_S) * (D_B) - Q_L * (D_L/Kp_L_D*(hemato*Kp_BC_D+1-hemato));

vdist_D_S @Reaction {actors: D_B => D_S, units: ug/h};
vdist_D_S := Q_S * (D_B);

vdist_D_SL @Reaction {actors: D_S => D_L, units: ug/h};
vdist_D_SL := Q_S * (D_S/Kp_S_D*(hemato*Kp_BC_D+1-hemato));

vdist_D_T @Reaction {actors: D_B <=> D_T, units: ug/h};
vdist_D_T := Q_T * (D_B-D_T/Kp_T_D*(hemato*Kp_BC_D+1-hemato));

vdist_D_K @Reaction {actors: D_B <=> D_K, units: ug/h};
vdist_D_K := Q_K * (D_B-D_K/Kp_K_D*(hemato*Kp_BC_D+1-hemato));

vdist_D_U @Reaction {actors: D_B <=> D_U, units: ug/h};
vdist_D_U := Q_U * (D_B-D_U/Kp_U_D*(hemato*Kp_BC_D+1-hemato));

vdist_D_R @Reaction {actors: D_B <=> D_R, units: ug/h};
vdist_D_R := Q_R * (D_B-D_R/Kp_R_D*(hemato*Kp_BC_D+1-hemato));

vcl_D_L @Reaction {actors: D_L => , units: ug/h};
vcl_D_L := V_L * (kel_L_D*D_L);

vcl_D_S @Reaction {actors: D_S => , units: ug/h};
vcl_D_S := zero_rate;

vcl_D_T @Reaction {actors: D_T => , units: ug/h};
vcl_D_T := zero_rate;

vcl_D_K @Reaction {actors: D_K => , units: ug/h};
vcl_D_K := zero_rate;

vcl_D_U @Reaction {actors: D_U => , units: ug/h};
vcl_D_U := zero_rate;

vcl_D_R @Reaction {actors: D_R => , units: ug/h};
vcl_D_R := zero_rate;

vcl_D_B @Reaction {actors: D_B => , units: ug/h};
vcl_D_B := zero_rate;

// processes
v_ir @Process {actors: <=> response, units: 1/h};
v_ir := syn_resp * D_P - deg_resp * response;

// reference population
'''Animal weight'''
W_animal_061 @Const 'reference mouse weight (U)' {units: g} = 20.8;
'''Tumor weight (reference value)'''
W_T_061 @Const {units: g} = 0.366;
'''Liver to whole animal weight ratio (reference value)'''
w_L_061 @Const {units: 1} = 0.0538;
'''Spleen to whole animal weight ratio (reference value)'''
w_S_061 @Const {units: 1} = 0.00601;
'''Kidney to whole animal weight ratio (reference value)'''
w_K_061 @Const {units: 1} = 0.0142;
'''Lungs to whole animal weight ratio (reference value)'''
w_U_061 @Const {units: 1} = 0.00679;
'''Blood to whole animal weight ratio'''
w_B_061 @Const {units: 1} = 8.14e-02; // fitted

// === compound-specific constants ===
// fitted based on in-vitro data
'''"Burst" coefficient for release'''
delta @Const {units: 1} = 1;
'''Diffusion coefficient for release'''
kdiff @Const {units: kdiffUnits} = 0;
'''Concentration-time coefficient for release'''
gamma @Const {units: 1} = 0;

// === experiment-specific constants ===
// population dependent
'''Animal weight'''
W_animal @Const 'mouse weight for U' {units: g};
'''Tumor weight'''
W_T @Const {units: g};
'''Liver to whole animal weight ratio'''
w_L @Const {units: 1};
'''Spleen to whole animal weight ratio'''
w_S @Const {units: 1};
'''Kidney to whole animal weight ratio'''
w_K @Const {units: 1};
'''Lungs to whole animal weight ratio'''
w_U @Const {units: 1};

'''Diffusion coefficient for kidney'''
PS_K_061 @Const {units: mL/h} = 9.27e-4;
'''Diffusion coefficient for liver'''
PS_L_061 @Const {units: mL/h} = 2.14e-2; // it was a wrong value from manuscript 2.02e-3;
'''Diffusion coefficient for rest of body'''
PS_R_061 @Const {units: mL/h} = 5.79e-2;
'''Diffusion coefficient for spleen'''
PS_S_061 @Const {units: mL/h} = 2.02e-3;
'''Diffusion coefficient for tumor'''
PS_T_061 @Const {units: mL/h} = 1.31e-3;
'''Diffusion coefficient for lungs'''
PS_U_061 @Const {units: mL/h} = 1.22e-4;

// === experiment-specific conditions ===
'''selected based on experimental conditions, per body weight'''
dose_D_ug_g @Const 'Injected dose of drug' {units: ug/g};

// === animal-specific constants ===
// animal: mouse, animal_id=1
w_B @Const {units: 1} = 8.14e-02; // fitted
'''Blood flux through liver'''
Q_L @Const {units: mL/h} = 181.92;
'''Blood flux through spleen'''
Q_S @Const {units: mL/h} = 14.88;
'''Blood flux through kidney'''
Q_K @Const {units: mL/h} = 124.6;
'''Blood flux through lungs'''
Q_U @Const {units: mL/h} = 678;
'''Blood flux through rest of body'''
Q_R @Const {units: mL/h} = 371.48;

'''Blood flux through tumor per gram of tumor'''
Q_T_g @Const {units: mL/h/g} = 2.06; // fitted on VCR
'''Coefficient of drug elimination in liver'''
kel_L_D @Const {units: 1/h} = 4.75; // fitted on VCR

'''Blood in organ to whole kidneys volume ratio'''
bto_K @Const {units: 1} = 8.58e-2; // fitted on U16c/p
'''Blood in organ to whole liver volume ratio'''
bto_L @Const {units: 1} = 1.41e-1;
'''Blood in organ to whole spleen volume ratio'''
bto_S @Const {units: 1} = 1.53e-1;
'''Blood in organ to whole tumor volume ratio'''
bto_T @Const {units: 1} = 9.96e-3;
'''Blood in organ to whole lungs volume ratio'''
bto_U @Const {units: 1} = 2.23e-1;

// === animal-specific constants ===
// constants
hemato @Const 'Hematocrit' {units: 1} = 0.45; // approx for all species
'''Equilibrium partition coefficient (blood cells vs plasma) for drug, drug-type dependent.'''
Kp_BC_D @Const {units: 1} = 3.9;
'''Partition coefficient for drug in spleen'''
Kp_S_D @Const {units: 1} = 64.8;
'''Partition coefficient for drug in kidneys'''
Kp_K_D @Const {units: 1} = 55.6;
'''Partition coefficient for drug in lungs'''
Kp_U_D @Const {units: 1} = 76.4;
'''Partition coefficient for drug in liver'''
Kp_L_D @Const {units: 1} = 42.0;
'''Partition coefficient for drug in rest of body'''
Kp_R_D @Const {units: 1} = 55.6; // Kp_R_D = Kp_K_D
'''Partition coefficient for drug in tumor'''
Kp_T_D @Const {units: 1} = 63.2;

'''Scaling factor for PS parameters'''
b_PS @Const {units: 1} = 1; // fixed
'''Coefficient describing responce action on kidneys'''
koef_resp_K @Const {units: 1} = 7.93e+1;
'''Coefficient describing responce action on liver'''
koef_resp_L @Const {units: 1} = 2.09e+2;
'''Coefficient describing responce action on rest of body'''
koef_resp_R @Const {units: 1} = 2.46e+2;

'''"Synthesis" of indirect response coefficient'''
syn_resp @Const {units: mL/ug/h} = 1; // fixed
'''"Degradation" of indirect response coefficient'''
deg_resp @Const {units: 1/h} = 2.46;

'''Constant equal to zero to pass units checking for reactions.'''
zero_rate @Const 'To fit the units checking' {units: ug/h} = 0;